apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.sky.xposed.app"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 2
        versionName "1.0.2"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        sky {
            storeFile file("${rootDir}/keyStore/sky.jks")
            storePassword project.storePassword
            keyAlias project.keyAlias
            keyPassword project.keyPassword
        }
    }
    buildTypes {
        release {
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${project.name}_${variant.flavorName}_v${variant.versionName}.apk"
                }
            }

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sky
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.sky
        }
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
//        dataBinding = true
        viewBinding = true
    }

    flavorDimensions "default"

    productFlavors {
        all { flavor ->
            resValue 'string', 'flavor', flavor.name
        }
        common {
            dimension "default"
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

greendao {
    schemaVersion 2
    daoPackage 'com.sky.xposed.load.data.db.dao'
    targetGenDir 'src/main/java'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Test
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    // Google
    implementation 'com.google.android.material:material:1.2.1'

    // GreenDao
    implementation 'org.greenrobot:greendao:3.3.0'

    // RxJava
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.11'

    // Binding
    implementation 'com.hi-dhl:binding:1.1.2'

    // Common
    implementation 'com.github.sky-wei.android-common:base:1.2.2'
    implementation 'com.github.sky-wei.android-common:core:1.2.2'
}
